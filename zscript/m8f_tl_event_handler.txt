
class m8f_tl_KeyboardHandler : EventHandler
{

  override void OnRegister()
  {
    _tradeManager = new("m8f_tl_TradeManager").init();
    _tradeView    = new("m8f_tl_TradeView").init();
  }

  override void NetworkProcess(ConsoleEvent event)
  {
    string eventName = event.name;

    if      (eventName == "m8f_tl_init_trade")  { _tradeManager.startTradeForPlayerAndTarget(); }
    else if (eventName == "m8f_tl_end_trade")   { _tradeManager.endTrade(); }
    else if (eventName == "m8f_tl_transaction")
    {
      _tradeManager.makeTransaction( _tradeView.isLeftSelected()
                                   , _tradeView.getLeftSelectedIndex()
                                   , _tradeView.getRightSelectedIndex()
                                   );
    }

    self.IsUiProcessor = _tradeManager.IsStarted();
  }

  override bool UiProcess(UiEvent event)
  {
    int type = event.type;
    if (type != UiEvent.Type_KeyDown) { return true; }

    int key = event.keyChar;

    switch (key)
    {
    case UiEvent.Key_Escape:
      SendNetworkEvent("m8f_tl_end_trade");
      break;

    case UiEvent.Key_Tab:
      _tradeView.toggleLeftRightSelection();
      break;

    case UiEvent.Key_Down:
      _tradeView.moveItemSelectionDown(_tradeManager);
      break;

    case UiEvent.Key_Up:
      _tradeView.MoveItemSelectionUp();
      break;

    case UiEvent.Key_Return:
      SendNetworkEvent("m8f_tl_transaction");
      break;
    }

    return true;
  }

  override void RenderOverlay(RenderEvent event)
  {
    _tradeView.show(_tradeManager);
  }

  // private attribute section /////////////////////////////////////////////////

  private m8f_tl_TradeManager _tradeManager;
  private m8f_tl_TradeView    _tradeView;

} // class m8f_tl_KeyboardHandler
